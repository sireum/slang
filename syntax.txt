<program>          ::=  <package>?
                        import org . sireum . _
                        <stmt>*
                        
<package>          ::=  package <name>

<name>             ::=  <ID> ⸨ . <ID> ⸩*
                   
<stmt>             ::=  <import-stmt>
                     |  <decl-stmt>
                     |  <assign-stmt>
                     |  <print-stmt>
                     |  <if-stmt>
                     |  <match-stmt>
                     |  <while-stmt>
                     |  <for-stmt>
                     |  <return-stmt>
                     |  <proof-stmt>

<import-stmt>      ::=  import <name> ⸨ . { <import-sel> ⸨ , <import-sel> ⸩* } ⸩?
                     |  import <ID> . _

<import-sel>       ::=  <ID>
                     |  <ID> => <ID>

<decl-stmt>        ::=  <val-decl>
                     |  <var-decl>
                     |  <def-decl>
                     |  <enum-decl>
                     |  <object-decl>
                     |  <sig-decl>
                     |  <datatype-decl>
                     |  <record-decl>
                     |  <rich-decl>
                     |  <type-decl>

<val-decl>         ::=  val <ID> ⸨ : <type> ⸩? = <stmt-exp>
                     |  val <pattern> = <stmt-exp>
                     |  @spec val <ID> : <type> = $

<var-decl>         ::=  var <ID> ⸨ : <type> ⸩? = <stmt-exp>
                     |  var <pattern> = <stmt-exp>
                     |  @spec var <ID> : <type> = $

<type>             ::=  <name> <type-args>?
                     |  ( <type> ⸨ , <type> ⸩+ )
                     |  <type> => <type>

<type-args>        ::=  [ <type> ⸨ , <type> ⸩* ]

<pattern>          ::=  <ID>
                     |  _
                     |  ⸨ <ID> @ ⸩? <name> ( <paterns>? )
                     |  ⸨ <ID> @ ⸩? ( <paterns> )

<patterns>         ::=  <pattern> ⸨ , <pattern> ⸩*

<def-decl>         ::=  ⸨ @pure ⸩? def <ID> <params>? : <type> = <def-body>
                     |  ⸨ @pure ⸩? def <ID> <params>? : <type> = $
                     |  ⸨ @pure ⸩? def <ID> <params>? : <type> = <ldef-contract>
                     |  ⸨ @pure ⸩? def <ID> <params>? : <type>
                     |  @spec def <ID> <params>? : <type> = $
                     |  @spec def <ID> <params>? : <type> = <ldef-contract>

<params>           ::=  ( ⸨ <param> ⸨ , param> ⸩* ⸩? )

<param>            ::=  ⸨ @pure ⸩? <ID> : <type>

<def-body>         ::=  {
                          <ldef-contract>?
                          <stmt>*                     
                        }

<enum-decl>        ::=  @enum object <ID> { val <ID> ⸨ , <ID> ⸩* = Value }

<object-decl>      ::=  object <ID> <extends>? { <stmt>* }
                     |  @ext object <ID> { <stmt>* }
                        
<extends>          ::=  extends <type> ⸨ with <type> ⸩*

<sig-decl>         ::=  @sig ⸨ sealed ⸩? trait <ID> <type-params>? <extends>? { <stmt>* }

<type-params>      ::=  [ <type-param> ⸨ , <type-param> ⸩* ]

<type-param>       ::=  <ID> ⸨ <: type ⸩?

<datatype-decl>    ::=  @datatype trait <ID> <type-params>? <extends>? { <stmt>* }
                     |  @datatype class <ID> <type-params>? <datatype-params>? <extends>? { <stmt>* }

<datatype-params>  ::=  ( ⸨ <datatype-param> ⸨ , <datatype-param> ⸩* ⸩? )

<datatype-param>   ::=  <adt-param-mod>* <ID> : <type>

<adt-param-mod>    ::=  @pure
                     |  @hidden

<record-decl>      ::=  @record trait <ID> <type-params>? <extends>? { <stmt>* }
                     |  @record class <ID> <type-params>? <record-params>? <extends>? { <stmt>* }

<record-params>    ::=  ( ⸨ <record-param> ⸨ , <record-param> ⸩* ⸩? )

<record-param>     ::=  <adt-param-mod>* ⸨ var ⸩*  <ID> : <type>

<rich-decl>        ::=  @rich trait <ID> <type-params> { <stmt>* }
                     |  @rich class <ID> <type-params>? <params>? <extends>? { <stmt>* }

<type-decl>        ::=  type <ID> <type-params>? = <type>

<assign-stmt>      ::=  <lhs> = <stmt-exp>
                     |  up ( <lhs> ) = <stmt-exp>
                     |  tup ( <lhs> ⸨ , <lhs> ⸩+ ) = <stmt-exp>

<lhs>              ::=  <ID>
                     |  <lhs> . <ID>
                     |  <lhs> ( <exp> )

<print-stmt>       ::=  print ( <print-arg> ⸨ , <print-arg> ⸩* )
                     |  println ( ⸨ <print-arg> ⸨ , <print-arg> ⸩* ⸩? )

<print-arg>        ::=  <exp>
                     |  <string-exp>

<if-stmt>          ::=  if ( <exp> ) { <stmt>* } ⸨ else { <stmt>* } ⸩?
                     |  if ( <exp> ) { <stmt>* } else <if-stmt>

<match-stmt>       ::=  <exp> match { <match-case>* }

<match-case>       ::=  case <lit> => <stmt>*
                     |  case <pattern> ⸨ if <exp> ⸩? => <stmt>*

<while-stmt>       ::=  while ( <exp> ) {
                          <loop-contract>?
                          <stmt>*
                        }
                     |  do {
                          <stmt>*
                          <loop-contract>?
                        } while ( <exp> )

<for-stmt>         ::=  for ( <for-gen> ) {
                          <loop-contract>?
                          <stmt>*
                        }

<for-gen>          ::=  <ID> <- <for-range>

<for-range>        ::=  <exp>
                     |  <exp> . indices
                     |  <exp> . reverseIndices
                     |  <exp> =-= <exp>
                     |  <exp> =-< <exp>
                     |  <exp> <-= <exp>
                     |  <exp> <-< <exp>
                     |  <exp> >-= <exp>
                     |  <exp> >-> <exp>

<return-stmt>      ::=  return <exp>?

<proof-stmt>       ::=  assert(<exp>)
                     |  assert(<exp>, <string-exp>)
                     |  assume(<exp>)
                     |  assume(<exp>, <string-exp>)
                     |  l""" <l-proof> """

<stmt-exp>         ::=  <exp>
                     |  <block-stmt-exp>
                     |  <if-stmt-exp>
                     |  <match-stmt-exp>

<block-stmt-exp>   ::=  { <stmt>* <exp> }

<if-stmt-exp>      ::=  if ( <exp> ) { <stmt>* <exp> } else { <stmt>* <exp> }
                     |  if ( <exp> ) { <stmt>* <exp> } else <if-stmt-exp>

<match-stmt>       ::=  <exp> match { ⸨ <match-case> <exp> ⸩* }

<string-exp>       ::=  <STRING>
                     |  <STRING-INTERP>

<exp>              ::=  <lit>
                     |  <string-exp>
                     |  <name>
                     |  <exp> _
                     |  ( <exp> ⸨ , <exp> ⸩* )
                     |  <exp> . <ID> <args>?
                     |  <exp> <args>
                     |  <unary-op> <exp>
                     |  <exp> <binary-op> <exp>
                     |  if ( <exp> ) <exp> else <exp>

<lit>              ::=  true
                     |  false
                     |  <INT>
                     |  <FLOAT>
                     |  z"<INT>"
                     |  z8"<INT>"
                     |  z16"<INT>"
                     |  z32"<INT>"
                     |  z64"<INT>"
                     |  n"<INT>"
                     |  n8"<INT>"
                     |  n16"<INT>"
                     |  n32"<INT>"
                     |  n64"<INT>"
                     |  s8"<INT>"
                     |  s16"<INT>"
                     |  s32"<INT>"
                     |  s64"<INT>"
                     |  u8"<INT>"
                     |  u16"<INT>"
                     |  u32"<INT>"
                     |  u64"<INT>"
                     |  f32"<FLOAT>"
                     |  f64"<FLOAT>"
                     |  r"<REAL>"

<args>             ::=  <type-args>? ( ⸨ <apply-arg> ⸨ , <apply-arg> ⸩* ⸩? )

<apply-arg>        ::=  <exp>
                     |  <exp> -> <exp>
                     |  <ID> = <exp>

<unary-op>         ::=  !
                     |  +
                     |  -
                     |  ~
                     |  ¬

<binary-op>        ::=  +
                     |  -
                     |  *
                     |  /
                     |  %
                     |  ==
                     |  !=
                     |  <<
                     |  >>
                     |  >>>
                     |  <
                     |  <=
                     |  >
                     |  >=
                     |  &
                     |  |
                     |  |^
                     |  ≡
                     |  ≠
                     |  ≤
                     |  ≥
                     |  ∧
                     |  ∨
                     |  ⊻
                     |  →

<l-proof>          ::=  <fact>
                     |  <invariant>
                     |  <sequent>
                     |  <proof>
                     |  <theorem>

<ldef-contract>    ::=  l""" <def-contract> """
