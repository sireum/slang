<unit>             ::=  <program>
                     |  <sequent>
                     |  <truth-table>

<program>          ::=  <package>?
                        import org . sireum . _
                        <stmt>*
                        
<package>          ::=  package <name>

<name>             ::=  <ID> ⸨ . <ID> ⸩*
                   
<stmt>             ::=  <import-stmt>
                     |  <decl-stmt>
                     |  <assign-stmt>
                     |  <block-stmt>
                     |  <if-stmt>
                     |  <match-stmt>
                     |  <while-stmt>
                     |  <for-stmt>
                     |  <return-stmt>
                     |  <exp>
                     |  <proof-stmt>

<import-stmt>      ::=  import <name> ⸨ . { <import-sel> ⸨ , <import-sel> ⸩* } ⸩?
                     |  import <name> . _

<import-sel>       ::=  <ID>
                     |  <ID> => <ID>

<decl-stmt>        ::=  <val-decl>
                     |  <var-decl>
                     |  <def-decl>
                     |  <enum-decl>
                     |  <object-decl>
                     |  <sig-decl>
                     |  <datatype-decl>
                     |  <record-decl>
                     |  <rich-decl>
                     |  <type-decl>

<val-decl>         ::=  val <ID> ⸨ : <type> ⸩? = <assign-exp>
                     |  @spec val <ID> : <type> = $
                     |  val <pattern> = <assign-exp>

<var-decl>         ::=  var <ID> ⸨ : <type> ⸩? = <assign-exp>
                     |  @spec var <ID> : <type> = $
                     |  var <pattern> = <assign-exp>

<def-decl>         ::=  ⸨ @pure ⸩? def <ID> <type-params>? <params>? : <type>
                     |  <def-mod> def <ID> <type-params>? <params>? : <type> = <def-body>
                     |  ⸨ @pure ⸩? def <ID> <type-params>? <params>? : <type> = $
                     |  ⸨ @pure ⸩? def <ID> <type-params>? <params>? : <type> = <ldef-contract>
                     |  @spec def <ID> <type-params>? <params>? : <type> = $
                     |  @spec def <ID> <type-params>? <params>? : <type> = <spec-ldef>

<def-mod>          ::=  @pure ⸨ override ⸩?
                     |  ⸨ @pure ⸩? override
                     |  @memoize

<enum-decl>        ::=  @enum object <ID> { <SYMBOL>* }

<object-decl>      ::=  object <ID> <extends>? { <stmt>* }
                     |  @ext object <ID> { <stmt>* }

<sig-decl>         ::=  @sig ⸨ sealed ⸩? trait <ID> <type-params>? <extends>? { <stmt>* }

<datatype-decl>    ::=  @datatype trait <ID> <type-params>? <extends>? { <stmt>* }
                     |  @datatype class <ID> <type-params>? <datatype-params>? <extends>? { <stmt>* }

<record-decl>      ::=  @record trait <ID> <type-params>? <extends>? { <stmt>* }
                     |  @record class <ID> <type-params>? <record-params>? <extends>? { <stmt>* }

<rich-decl>        ::=  @rich trait <ID> <type-params> <extends>? { <stmt>* }
                     |  @rich class <ID> <type-params>? <params>? <extends>? { <stmt>* }

<type-decl>        ::=  type <ID> <type-params>? = <type>

// Notes:
// Built-in basic types: B, <int-type>, F32, F64, R, Unit
// Built-in <int-type>: Z, Z8, Z16, Z32, Z64, N, N8, N16, N32, N64, S8, S16, S32, S64, U8, U16, U32, U64
// Built-in sequences: IS [ <int-type> , <type> ], ISZ [ <type> ], MS [ <int-type> , <type> ], MSZ [ <type> ]

<type>             ::=  <name> <type-args>?
                     |  ( <type> ⸨ , <type> ⸩+ )
                     |  <type> => <type>

<type-args>        ::=  [ <type> ⸨ , <type> ⸩* ]

<assign-exp>       ::=  <exp>
                     |  <block-assign-exp>
                     |  <if-assign-exp>
                     |  <match-assign-exp>

<pattern>          ::=  <lit>
                     |  <name>
                     |  <ID> : <type>
                     |  _ ⸨ : <type> ⸩?
                     |  _*
                     |  ⸨ <ID> @ ⸩? <name> ( <patterns>? )
                     |  ⸨ <ID> @ ⸩? ( <patterns> )

<patterns>         ::=  <pattern> ⸨ , <pattern> ⸩*

<type-params>      ::=  [ <type-param> ⸨ , <type-param> ⸩* ]

<type-param>       ::=  <ID> ⸨ <: type ⸩?

<params>           ::=  ( ⸨ <param> ⸨ , param> ⸩* ⸩? )

<param>            ::=  ⸨ <param-mod> ⸩? <ID> : <type>

<param-mod>        ::= @pure
                     | @hidden // only for @memoize def

<def-body>         ::=  {
                          <ldef-contract>?
                          <stmt>*
                        }

<extends>          ::=  extends <type> ⸨ with <type> ⸩*

<datatype-params>  ::=  ( ⸨ <datatype-param> ⸨ , <datatype-param> ⸩* ⸩? )

<datatype-param>   ::=  <adt-param-mod>? <ID> : <type>

<adt-param-mod>    ::=  @pure ⸨ @hidden ⸩?
                     |  @hidden ⸨ @pure ⸩?

<record-params>    ::=  ( ⸨ <record-param> ⸨ , <record-param> ⸩* ⸩? )

<record-param>     ::=  <adt-param-mod>* ⸨ var ⸩?  <ID> : <type>

<assign-stmt>      ::=  <lhs> = <assign-exp>
                     |  up ( <lhs> ) = <assign-exp>
                     |  pat ( <pattern> ) = <assign-exp>

<lhs>              ::=  <ID>
                     |  <lhs> . <ID>
                     |  <lhs> ( <exp> )

<if-stmt>          ::=  if ( <exp> ) { <stmt>* } ⸨ else { <stmt>* } ⸩?
                     |  if ( <exp> ) { <stmt>* } else <if-stmt>

<match-stmt>       ::=  <exp> match { <match-case>+ }

<case>             ::=  case <pattern> ⸨ if <exp> ⸩? => <stmt>*

<while-stmt>       ::=  while ( <exp> ) {
                          <loop-contract>?
                          <stmt>*
                        }
                     |  do {
                          <stmt>*
                          <loop-contract>?
                        } while ( <exp> )

<for-stmt>         ::=  for ( <for-enum-gen> ) {
                          <loop-contract>?
                          <stmt>*
                        }

<for-enum-gen>     ::=  <ID> <- <for-range> ⸨ if <exp> ⸩?

<for-range>        ::=  <exp>
                     |  <exp> . reverse
                     |  <exp> . indices
                     |  <exp> . indices . reverse
                     |  <exp> until <exp> ⸨ by <exp> ⸩?
                     |  <exp> to <exp> ⸨ by <exp> ⸩?

<return-stmt>      ::=  return <exp>?

<proof-stmt>       ::=  l""" <l-clause> """

<block-assign-exp> ::=  { <stmt>* <exp> }

<if-assign-exp>    ::=  if ( <exp> ) <block-assign-exp> else <block-assign-exp>
                     |  if ( <exp> ) <block-assign-exp> else <if-assign-exp>

<match-assign-exp> ::=  <exp> match { ⸨ <match-case> <exp> ⸩+ }

<exp>              ::=  <lit>
                     |  <STRING-INTERP>
                     |  <name>
                     |  this
                     |  <exp> _
                     |  <unary-op> <exp>
                     |  <exp> <binary-op> <exp>
                     |  ( <exp> ⸨ , <exp> ⸩* )
                     |  <exp> . <ID> <args>?
                     |  <exp> <args>
                     |  if ( <exp> ) <exp> else <exp>
                     |  for ( <for-enum-gen> ⸨ ; <for-enum-gen> ⸩* ) yield <exp>
                     |  <quant-op> <ID> ⸨ , <ID> ⸩* : <domain> <exp>  // only in the proof context

<quant-op>         ::=  ∀
                     |  A
                     |  all
                     |  forall
                     |  ∃
                     |  E
                     |  some
                     |  exists

<domain>           ::=  <type>
                     |  <lrange> <exp> , <exp> <rrange>

<lrange>           ::=  [
                     |  (

<rrange>           ::=  ]
                     |  )

<lit>              ::=  true
                     |  false
                     |  <CHAR>
                     |  <INT>
                     |  <LONG>
                     |  z"<NUM>"
                     |  z8"<NUM>"
                     |  z16"<NUM>"
                     |  z32"<NUM>"
                     |  z64"<NUM>"
                     |  n"<NUM>"
                     |  n8"<NUM>"
                     |  n16"<NUM>"
                     |  n32"<NUM>"
                     |  n64"<NUM>"
                     |  s8"<NUM>"
                     |  s16"<NUM>"
                     |  s32"<NUM>"
                     |  s64"<NUM>"
                     |  u8"<NUM>"
                     |  u16"<NUM>"
                     |  u32"<NUM>"
                     |  u64"<NUM>"
                     |  f32"<FLOAT>"
                     |  f64"<DOUBLE>"
                     |  r"<REAL>"
                     |  bb"<INT>" [ <int-type> ]
                     |  bl"<INT>" [ <int-type> ]
                     |  <FLOAT>
                     |  <DOUBLE>
                     |  <STRING>

<args>             ::=  <type-args>? ( ⸨ <apply-arg> ⸨ , <apply-arg> ⸩* ⸩? )

<apply-arg>        ::=  <exp>
                     |  <exp> -> <exp>
                     |  <ID> = <exp>

// Note: Unicode operators are only available in the proof context

<unary-op>         ::=  +
                     |  -
                     |  <neg-op>

<binary-op>        ::=  +
                     |  -
                     |  *
                     |  /
                     |  %
                     |  ≡
                     |  ==
                     |  ≠
                     |  !=
                     |  <<
                     |  >>
                     |  >>>
                     |  <
                     |  ≤
                     |  <=
                     |  >
                     |  ≥
                     |  >=
                     |  <and-op>
                     |  <or-op>
                     |  <xor-op>
                     |  <imply-op>  // right-associative
                     |  :+          // append
                     |  +:          // prepend, right-associative
                     |  ++          // append all
                     |  --          // remove all

<and-op>           ::=  ∧
                     |  ^
                     |  &

<or-op>            ::=  ∨
                     |  V   // (i.e., capital V)
                     |  |

<xor-op>           ::=  ⊻
                     |  |^

<imply-op>         ::=  →
                     |  ->  // only in the proof context

<neg-op>           ::=  ¬
                     |  ~
                     |  !

<ldef-contract>    ::=  l""" <def-contract> """

<loop-contract>    ::=  l""" <loop-inv-mod> """

<spec-ldef>        ::=  l""" <spec-defs> """

<def-contract>     ::=  ⸨ reads <exp> ⸨ , <exp> ⸩* ⸩?
                        ⸨ requires <named-exps> ⸩?
                        ⸨ modifies <exp> ⸨ , <exp> ⸩* ⸩?
                        ⸨ ensures <named-exps> ⸩?
                        <sub-contract>*

<named-exps>       ::=  ⸨ ⸨ <ID> : ⸩? <exp> ⸩+  // newline separated

<sub-contract>     ::=  def <ID> ( ⸨ @pure ⸩? <ID> ⸨ , ⸨ @pure ⸩? <ID> ⸩* ) <def-contract>


<spec-defs>        ::=  <spec-def>+            // newline separated
                        where <where-def>+     // newline separated

<spec-def>         ::=  = <named-exp> ⸨ , <spec-def-cond> ⸩?

<spec-def-cond>    ::=  ⸨ case <pattern> ⸩? if <exp>
                     |  otherwise

<where-def>        ::=  val <ID> : <type> = <exp>
                     |  def <ID> ( <spec-param> ⸨ , <spec-param> ⸩* ) : <type>
                        <spec-def>+            // newline separated

<spec-param>       ::=  <ID> : <type>


<loop-inv-mod>     ::=  ⸨ invariant <named-exps> ⸩?
                        ⸨ modifies <exp> ⸨ , <exp> ⸩* ⸩?

<l-clause>         ::=  <invariants>
                     |  <facts>
                     |  <theorems>
                     |  <sequent>
                     |  <proof>

<invariants>       ::=  invariant <named-exps>

<facts>            ::=  fact ⸨ <ID> : <exp> ⸩+   // newline separated

<theorems>         ::=  theorem ⸨ <ID> : <sequent> ⸩+

<sequent>          ::=  ⸨ <exp> ⸨  , <exp> ⸩* ⸩? <sequent-op> <exp> ⸨  , <exp> ⸩* <proof>?

<sequent-op>       ::=  ⊢
                     |  |-

<proof>            ::=  {
                        <proof-step>+            // newline separated
                        }

<proof-step>       ::=  <NUM> . <exp> <just>
                     |  <NUM> . <sub-proof>

<sub-proof>        ::=  {
                        <assume-step>            // newline separated
                        <proof-step>
                        }

<assume-step>      ::=  <NUM> . <exp> assume
                     |  <NUM> . ID ⸨ , <ID> ⸩* ⸨ <exp> assume ⸩?
                     |  <NUM> . ID ⸨ , <ID> ⸩* : <domain> ⸨ ID ⸨ , <ID> ⸩* : <domain> ⸩* ⸨ <exp> assume ⸩?

<just>             ::=  premise
                     |  <and-op> i      <NUM> <NUM>+
                     |  <and-op> e1     <NUM>
                     |  <and-op> e2     <NUM>
                     |  <or-intro1>     <NUM>
                     |  <or-intro2>     <NUM>
                     |  <or-elim>       <NUM>
                     |  <imply-op> i    <NUM>
                     |  <imply-op> e    <NUM> <NUM>+
                     |  <neg-op> i      <NUM>
                     |  <neg-op> e      <NUM> <NUM>
                     |  <bottom> e      <NUM>
                     |  pbc             <NUM>
                     |  <forall-intro>  <NUM>
                     |  <forall-elim>   <NUM> <exp> ⸨ , <exp> ⸩*
                     |  <exists-intro>  <NUM> <exp> ⸨ , <exp> ⸩*
                     |  <exists-elim>   <NUM> <NUM>
                     |  fact            <ID> ⸨ . <ID> ⸩*
                     |  invariant       ⸨ <ID> ⸨ . <ID> ⸩* ⸩?
                     |  subst <         <NUM> <NUM>
                     |  subst >         <NUM> <NUM>
                     |  algebra         <NUM>*
                     |  auto            <NUM>*
                     |  coq>            <STRING> <NUM>*

<or-intro1>        ::=  <or-op> i1
                     |  Vi1

<or-intro2>        ::=  <or-op> i2
                     |  Vi2

<or-elim>          ::=  <or-op> e
                     |  Ve

<bottom>           ::=  ⊥
                     |  _|_

<forall-intro>     ::=  ∀ i
                     |  Ai
                     |  alli
                     |  foralli

<forall-elim>      ::=  ∀ e
                     |  Ae
                     |  alle
                     |  foralle

<exists-intro>     ::=  ∃ i
                     |  Ei
                     |  somei
                     |  existsi

<exists-elim>      ::=  ∃ e
                     |  Ee
                     |  somee
                     |  existse

<truth-table>      ::=  ⸨ * ⸩*
                        <hline>
                        <ID>* | <sequent-or-exp>     // sequent without proof
                        <hline>
                        <truth-table-row>*           // newline separated
                        <hline>
                        ⸨ <truth-table-conc> ⸩*

<hline>            ::=  ----                         // can be more than four dashes

<truth-table-row>  ::=  <blit>* | <blit>+            // <blit> can be sequenced without whitespace

<blit>             ::=  T
                     |  F

<truth-table-conc> ::=  Invalid ⸨ [ <blit>+ ] ⸩*
                     |  Invalid ⸨ [ <blit>+ ] ⸩*
                     |  Tautology
                     |  Contradictory
                     |  Contingent
                        ⸨ - ⸩? T : ⸨ [ <blit>+ ] ⸩*
                        ⸨ - ⸩? F : ⸨ [ <blit>+ ] ⸩*
